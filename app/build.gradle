apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
//公共函数的多渠道配置
apply from: "${getProjectDir().getParentFile().getPath() + "/flavors_a00.gradle"}"
apply from: "${getProjectDir().getParentFile().getPath() + "/flavors_a01.gradle"}"
apply from: "${getProjectDir().getParentFile().getPath() + "/flavors_a02.gradle"}"

//方便修改打包后的名称
def apkNamesList = new ArrayList<String>()

//android配置
android {
  compileSdkVersion 29
  //基础配置
  defaultConfig {
    applicationId "com.cc.config"
    minSdkVersion 23
    targetSdkVersion 29
    versionCode 1
    versionName "1.0"
    resValue "string", "build_time", new Date().format("yyyy_MM_dd HH:mm")
    multiDexEnabled true
    resConfigs "zh", "en" //保留中文和英文资源
    ndk { abiFilters 'armeabi-v7a' } //,'arm64-v8a'
    flavorDimensions "versionCode"
  }

  //编译类型
  buildTypes {
    debug {
      debuggable true
      zipAlignEnabled false
      shrinkResources false
      minifyEnabled false
      //指定 debug 使用release的签名
      signingConfig release.signingConfig
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
    release {
      debuggable false
      zipAlignEnabled true
      shrinkResources true
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }

  //lambda表达式需要
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  //多维度 https://blog.csdn.net/chen_xi_hao/article/details/80526049
  flavorDimensions "default"
  //代码差分化打包
  productFlavors {
    apkNamesList.add(applyFlavorsA00("default"))
    apkNamesList.add(applyFlavorsA01("default"))
    apkNamesList.add(applyFlavorsA02("default"))
  }
}

//三方依赖
dependencies {
  //为了保证代码能正常编译，使用compileOnly，真正的libs到渠道配置里面去配置
  compileOnly fileTree(dir: "libs", include: ["*.jar", '*.aar'])
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  implementation 'androidx.core:core-ktx:1.3.2'
  implementation 'androidx.appcompat:appcompat:1.2.0'
  implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
  //https://github.com/Blankj/AndroidUtilCode/blob/master/lib/utilcode/README-CN.md
  implementation 'com.blankj:utilcodex:1.30.6'
}

//打包处理
android.applicationVariants.all { variant ->
  variant.outputs.all { output ->
    //正式版还是测试版
    String typeName = buildType.name
    typeName = typeName.substring(0, 1).toUpperCase() + typeName.substring(1).toLowerCase()
    //版本号
    String versionName = getVersionName()
    //渠道名称
    String flavorsName = variant.productFlavors[0].name
    //应用名称
    String apkName = ""
    apkNamesList.find { name ->
      if (name.trim().startsWith(flavorsName) && name.contains(",")) {
        apkName = name.split(",")[1]
        return true
      }
    }
    //打包完成后的重命名和拷贝
    assemble.doLast {
      //创建APK目录(APK+渠道名称)
      File apkFile = new File("../APK/${flavorsName}/${typeName}")
      if (!apkFile.exists()) apkFile.mkdirs()
      //编译完成的时间
      String buildEndTime = "${new Date().format("yyyyMMddHHmm")}"
      String fileName = "${apkName}${typeName}_${versionName}_${buildEndTime}.apk"
      String delPath = ""
      //把正式版拷贝到项目APK目录
      if (typeName == "Release" || rootProject.ext.needCreateDebug == "true") {
        project.copy {
          from("${output.outputFile}")
          into("${apkFile.path}")
          rename("${output.outputFile.name}", "${fileName}")
        }
      } else {
        System.printf("\nAPK编译完成:%s\n", output.outputFile.path)
        String temDir = "${new File(outputFile.parent).parent}/APK"
        delPath = new File(outputFile.parent).path
        delete(temDir)
        project.copy {
          from("${output.outputFile}")
          into(temDir)
          rename("${output.outputFile.name}", "${fileName}")
        }
      }
      //开启线程，在打包完成后20秒杀掉JDK，防止下次运行出现被占用的情况
      new Thread() {
        @Override void run() {
          sleep(5 * 1000)
          if (delPath.length() > 0) delete(delPath)
          sleep(15 * 1000)
          String cmd = "taskkill /f /t /im java.exe"
          cmd.execute().text.trim()
        }
      }.start()
    }
  }
}